(import iso8601ForFs)

(const
  loadJsonArrayElementsFromFiles
  (async.generatorFn (paths)
    (forEach p paths
      (forEach e (JSON.parse (async.await (fs.readFile p "utf-8")))
        [ (yield [e p])]))))

(annotate
  "Utility function that should be in the standard library of Custard."
  (const
    slicesOf
    (async.generatorFn (iterator n)
      (let current [])
      (async.forEach e iterator
        (array.push current e)
        (when (equals current.length n)
          (yield current)
          (assign current [])))
      (when (isGreaterThan current.length 0)
        (yield current)))))

(const targetPaths (array.slice process.argv 2))
(targetPaths.sort)

(async.forEach slice
  (slicesOf (loadJsonArrayElementsFromFiles targetPaths) 20)

  (const
    [latestIndexedAt p]
    (array.reduce
      (array.map slice (fn ([po pa]) [po.indexedAt pa]))
      (fn (a b)
        (if
          (isGreaterThan
            (new Date (array.first a))
            (new Date (array.first b)))
          a else b))))

  (const
    outName
    (path.join
      (path.dirname p)
      (iso8601ForFs.toFileName latestIndexedAt ".json")))

  (console.log "Writing" outName)
  (annotate
    "Rename the destination file. If it doesn't exist, that's fine."
    (async.await
      (async.scope
        (try
          (async.await
            (fs.rename outName (text outName ".bk")))
          catch e
          (when (notEquals e.code "ENOENT")
            (throw e))))))

  (const
    postsInSlice
    (array.map slice (fn ([post _]) post)))
  (async.await
    (fs.writeFile outName (JSON.stringify postsInSlice none 2))))
